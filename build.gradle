buildscript {
    repositories {
        mavenCentral()
        maven {url='https://repo.spongepowered.org/repository/maven-public'}
    }
    dependencies {
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT', changing: true
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '6.0+'
    id 'com.matthewprenger.cursegradle' version '1.2.0'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'de.undercouch.download' version '3.3.0'
    id 'idea'
    id "com.modrinth.minotaur" version "2.+"
    id 'eclipse'
}

apply from: 'https://raw.githubusercontent.com/SizableShrimp/ForgeUpdatesRemapper/main/remapper.gradle'

group = 'de.teamlapen.werewolves' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'Werewolves'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

// Declare here to use in gradle/versioning.gradle
ext.mod_version="Test"
ext.type="test"
ext.classification = "test"

sourceSets {
    api
    main {
        resources {
            srcDirs += 'src/generated/resources'
            exclude '.cache'
        }
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output

        java {
            exclude '**/modcompat/jei/**'
        }
    }
}

apply from: 'gradle/versioning.gradle'
apply from: 'gradle/forge.gradle'
apply from: 'gradle/artifacts.gradle'
apply from: 'gradle/deploy.gradle'

configurations {
    apiCompile.extendsFrom(compile)
    apiImplementation.extendsFrom(implementation)
    apiRuntimeOnly.extendsFrom(runtimeOnly)
}


idea {
    module {
        for (String excludeDirName in ["run", "out", "logs", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
        downloadJavadoc = true
        downloadSources = true
    }
}